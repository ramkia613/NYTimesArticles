apply plugin: 'com.android.application'
apply plugin: 'jacoco'
android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.nytimes.articles"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    jacoco {
        version = "$jacocoVersion"
    }

    task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
        group = "Reporting"
        description = "Generate Jacoco coverage reports for Debug build"

        reports {
            xml.enabled = true
            html.enabled = true
        }

        // what to exclude from coverage report
        // UI, "noise", generated classes, platform classes, etc.
        def excludes = ['**/R*.class',
                        '**/BuildConfig.*',
                        '**/Manifest*.*',
                        '**/model/*.*'
        ]

        // generated classes
        classDirectories = fileTree(
                dir: "$buildDir/intermediates/classes/debug",
                excludes: excludes
        )

        // sources
        sourceDirectories = files(["$projectDir/src/main/java"])
        executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //support lib
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintVersion"

    implementation "android.arch.lifecycle:extensions:$archVersion"

    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$gsonConverterVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$httpLoggerVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "de.hdodenhof:circleimageview:$circleImageVersion"

    implementation 'com.android.support:support-v4:27.1.1'
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-all:$mockitoVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-support-v4:$shadowsSupportV4"
}
